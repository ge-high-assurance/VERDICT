package CruiseController
public
	with Base_Types;
	with VERDICT_Properties;

	annex agree {**

		const PI: real = 3.1416;
		const ONE_MILE: real = 1609.34; -- meters

		const WHEEL_DIAMETER: real = 0.52; -- meters
		const K: real = (3600.0/ONE_MILE)*(PI*WHEEL_DIAMETER);

		const MIN_SPEED: real = 25.0; -- mph
		const MAX_SPEED: real = 130.0; -- mph
		
		const MIN_FILTERED_SPEED: real = 20.0; -- mph
		const MAX_FILTERED_SPEED: real = 140.0; -- mph
		const FILTER_EXPECTED_DIFFERENCE: real = 10.0; -- mph
	
		const SPEED_CHANGE_STEP: real = 1.0;
		const FORCE_COEFFICIENT: real = 0.1;

		node ToInt(b: bool) returns (i: int);
		let
			i = if b then 1 else 0;
		tel;
		
		node Max(a: real, b: real) returns (z: real);
		let
			z = if a >= b then a else b;
		tel;
		
		node UnchangedReal(n:real) returns (b:bool);
		let
 			b = true -> n = pre(n);
		tel;
		
		node InitiallyHolds(X:bool) returns (Y:bool);
		let
		  Y = X -> true;
		tel;
		
		node Abs(x:real) returns(y: real);
		let
   			y = if (x >= 0.0) then x else -x;
		tel;
		
		node Since(X: bool, Y: bool) returns (Z: bool);
		let
			Z = X or (Y and (false -> pre(Z)));
		tel;
	**};

	system SetSpeed
		features
			on_off: in data port Base_Types::Boolean;
			resume_speed: in data port Base_Types::Boolean;
			set_speed: in data port Base_Types::Boolean;
			inc: in data port Base_Types::Boolean;
			dec: in data port Base_Types::Boolean;
			brake_cancel: in data port Base_Types::Boolean;
			speed: in data port Base_Types::Float;
			desired_speed: out data port Base_Types::Float
			{VERDICT_Properties::probe => true;};
			enabled: out data port Base_Types::Boolean;
		annex agree {**
			
			-- Modes
			eq off_mode: bool = true -> 
								(pre(off_mode) and not on_off) or
								(pre(ready_mode) and on_off) or
								(pre(on_mode) and on_off) or 
								(pre(paused_mode) and on_off);
			
			eq set_condition: bool = set_speed and MIN_SPEED <= speed and speed <= MAX_SPEED;
			
			eq ready_mode: bool = false ->
								  (pre(ready_mode) and not on_off and not set_condition) or
								  (pre(off_mode) and on_off);
			
			eq on_mode: bool = false ->
							   (pre(on_mode) and not on_off and not brake_cancel) or
							   (pre(ready_mode) and set_condition) or
							   (pre(paused_mode) and resume_speed);   
			
			eq paused_mode: bool = false ->
								   (pre(paused_mode) and not on_off and not resume_speed) or
								   (pre(on_mode) and brake_cancel);
			
			-- Assumptions
			assume "Speed is always a non-negative value": speed >= 0.0;
			
			assume "Buttons are mutually exclusive": 
					ToInt(on_off) + 
					ToInt(resume_speed) + ToInt(set_speed) + 
					ToInt(inc) + ToInt(dec) + ToInt(brake_cancel) <= 1;
			
			guarantee "Cruise control is enabled if and only if it is On":
					  enabled = on_mode;
			
			eq setting_condition: bool =
				false -> pre(ready_mode) and set_condition;
					  
			guarantee "Desired speed setting":
				setting_condition => desired_speed = speed; 
			
			eq increment_condition: bool =
				false -> pre(on_mode) and inc and pre(desired_speed) <= MAX_SPEED - SPEED_CHANGE_STEP;
			
			guarantee "Desired speed increment":
				true -> (increment_condition => desired_speed = pre(desired_speed) + SPEED_CHANGE_STEP);
			
			eq decrement_condition: bool =
				false -> pre(on_mode) and dec and pre(desired_speed) >= MIN_SPEED + SPEED_CHANGE_STEP;
			
			guarantee "Desired speed decrement":
				true -> (decrement_condition => desired_speed = pre(desired_speed) - SPEED_CHANGE_STEP);
			
			eq invariance_condition: bool =
				not setting_condition and not increment_condition and not decrement_condition;
			
			guarantee "Desired speed invariance":
				true -> (invariance_condition => desired_speed = pre(desired_speed));				
		**};
	end SetSpeed;
	
	system ControlSpeed
		features
			speed: in data port Base_Types::Float;
			desired_speed: in data port Base_Types::Float;
			force: out data port Base_Types::Float;
		annex agree {**
			-- Assumptions
			assume "Speed is always a non-negative value": speed >= 0.0;
				
			-- Guarantees			
			guarantee "Force is zero when desired speed is not greater than the current speed":
				desired_speed <= speed => force = 0.0;
			
			guarantee "Force is bounded by a proportional factor of the speed difference":
				force >= Max(0.0, FORCE_COEFFICIENT * (desired_speed - speed));
		**};
	end ControlSpeed;

	system MeasureSpeed
		features
			rotate: in event port;
			second: in event port;
			speed: out data port Base_Types::Float;
		annex agree {**
			eq nc: int = 0 -> pre(count) + (if event(rotate) then 1 else 0);
			eq count: int = if event(second) then 0 else nc;
			guarantee "Speed is initially zero": InitiallyHolds(speed = 0.0);
			guarantee "Speed is updated every second":
							true -> (event(second) => speed = ( K * real(nc)));
			guarantee "Speed stays unchanged when it is not updated":
							true -> (not event(second) => speed = pre(speed));
		**};
	end MeasureSpeed;

	system Filter
		features
			is_enabled: in data port Base_Types::Boolean;
			desired_speed: in data port Base_Types::Float;
			filtered_desired_speed: out data port Base_Types::Float;
			
		annex agree {**
			
			eq desired_speed_is_valid: bool = desired_speed >= MIN_FILTERED_SPEED and desired_speed <= MAX_FILTERED_SPEED;

			-- Guarantees			
			guarantee "If filter is not enabled, just relay the desired speed":
				not is_enabled => filtered_desired_speed = desired_speed;
			
			guarantee "If filter is enabled and incomming desired speed is within the expected range, then just relay it to outgoing filtered desired speed":
				is_enabled => (desired_speed_is_valid => filtered_desired_speed = desired_speed);
				
			guarantee "If filter is enabled but incomming desired speed is not within the expected range, then smooth the filtered desired speed into the expected range":
				is_enabled => (desired_speed < MIN_FILTERED_SPEED => filtered_desired_speed = MIN_FILTERED_SPEED);
			
			guarantee "If filter is enabled but incomming desired speed is not within the expected range, then smooth the filtered desired speed into the expected range":
				is_enabled => (desired_speed > MAX_FILTERED_SPEED => filtered_desired_speed = MAX_FILTERED_SPEED);
			
			guarantee "If filter is enabled but the difference of incomming desired speed and previous filtered speed is more than 10, then adjust it to the expected difference":
				is_enabled => true -> ((desired_speed - pre(filtered_desired_speed)) > FILTER_EXPECTED_DIFFERENCE and (pre(filtered_desired_speed) + FILTER_EXPECTED_DIFFERENCE) <= MAX_FILTERED_SPEED
					          => filtered_desired_speed = (pre(filtered_desired_speed) + FILTER_EXPECTED_DIFFERENCE)
				);
			guarantee "If filter is enabled but the difference of incomming desired speed and previous filtered speed is more than 10, then adjust it to the expected difference":
				is_enabled => true -> ((desired_speed - pre(filtered_desired_speed)) > FILTER_EXPECTED_DIFFERENCE and (pre(filtered_desired_speed) + FILTER_EXPECTED_DIFFERENCE) > MAX_FILTERED_SPEED
					          => filtered_desired_speed = MAX_FILTERED_SPEED
				);	
			guarantee "If filter is enabled but the difference of incomming desired speed and previous filtered speed is more than 10, then adjust it to the expected difference":
				is_enabled => true -> ((pre(filtered_desired_speed) - desired_speed) > FILTER_EXPECTED_DIFFERENCE and (pre(filtered_desired_speed) - FILTER_EXPECTED_DIFFERENCE) >= MIN_FILTERED_SPEED
					          => filtered_desired_speed = (pre(filtered_desired_speed) - FILTER_EXPECTED_DIFFERENCE)
				);
			guarantee "If filter is enabled but the difference of incomming desired speed and previous filtered speed is more than 10, then adjust it to the expected difference":
				is_enabled => true -> ((pre(filtered_desired_speed) - desired_speed) > FILTER_EXPECTED_DIFFERENCE and (pre(filtered_desired_speed) - FILTER_EXPECTED_DIFFERENCE) < MIN_FILTERED_SPEED
					          => filtered_desired_speed = MIN_FILTERED_SPEED
				);
		**};
			
	end Filter;
	
	system CruiseControllerSystem
		features
			on_off: in data port Base_Types::Boolean;
			resume_speed: in data port Base_Types::Boolean;
			set_speed: in data port Base_Types::Boolean;
			inc: in data port Base_Types::Boolean;
			dec: in data port Base_Types::Boolean;
			brake_cancel: in data port Base_Types::Boolean;
			rotate: in event port;
			second: in event port;
			filter_is_enabled: in data port Base_Types::Boolean;
			force: out data port Base_Types::Float;
			enabled: out data port Base_Types::Boolean;

			-- Probe signals, not part of the actual system output
			speed: out data port Base_Types::Float;
			desired_speed: out data port Base_Types::Float;
		annex agree {**

			eq disabled: bool = not enabled;
			
			eq no_button_pressed: bool =
				not on_off and not resume_speed and not set_speed and
				not inc and not dec and not brake_cancel;
			
			-- Assumptions
			assume "Buttons are mutually exclusive": 
					ToInt(on_off) + ToInt(resume_speed) + ToInt(set_speed) + 
					ToInt(inc) + ToInt(dec) + ToInt(brake_cancel) <= 1;
					
			assume "Filter status":
				    not filter_is_enabled;	 -- enable or disable the filter here
				
			-- Properties
			guarantee "1. Cruise Controller System is enabled only if desired speed is within Cruise Controller minimum and maximun speed":
					   enabled => desired_speed >= MIN_SPEED and desired_speed <= MAX_SPEED;
			
			guarantee "2. Cruise Controller System with its filter is enabled only if desired speed is within Cruise Controller filtered minimum and maximun speed":
					   enabled => desired_speed >= MIN_FILTERED_SPEED and desired_speed <= MAX_FILTERED_SPEED;
			
			guarantee "3. Cruise Controller System with its filter is enabled only if difference between two consecutive desired speeds are not greater than the filter expected difference":
					   disabled or Since(enabled, Abs(desired_speed - pre(desired_speed)) <= FILTER_EXPECTED_DIFFERENCE);
			
			guarantee "4. Cruise Controller System is disabled initially":
					   InitiallyHolds(disabled);
			
			guarantee "5. When Cruise Controller System is disabled, the desired speed does not get changed":
					   disabled => UnchangedReal(desired_speed);
			
			guarantee "6. When Cruise Controller System is enabled and speed increase request does not exceed the maximum cruise controller speed, the desired speed is then increased accordingly":
					   true -> enabled and inc and pre(desired_speed) <= (MAX_SPEED - SPEED_CHANGE_STEP) =>
					   desired_speed = (pre(desired_speed) + SPEED_CHANGE_STEP);	
			
			guarantee "7. When Cruise Controller System is enabled and speed decrease request does not make desired speed become less than the minimum cruise controller speed, the desired speed is then decreased accordingly":
					   true -> enabled and dec and pre(desired_speed) > (MIN_SPEED + SPEED_CHANGE_STEP) =>
					   desired_speed = (pre(desired_speed) - SPEED_CHANGE_STEP);	
			
			guarantee "8. When Cruise Controller System is enabled and no button is pressed, the desired speed does not get changed":
					   enabled and no_button_pressed => UnchangedReal(desired_speed);		  
		
			guarantee "9. Force is zero if Cruise Controller System is enabled and the desired speed is not greater than the current speed":
						enabled and desired_speed <= speed => force = 0.0;
			
			guarantee "10. Force is positive if Cruise Controller System is enabled and the desired speed is greater than the current speed":
						enabled and desired_speed > speed => force > 0.0;
		**};
	end CruiseControllerSystem;
	
	system implementation CruiseControllerSystem.Impl
		subcomponents
			measureSpeed: system MeasureSpeed
			{
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => true;
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => InternallyDeveloped;
				VERDICT_Properties::adversariallyTestedForTrojanOrLogicBomb => 7;
			};
			setSpeed: system SetSpeed
			{
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => false;
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => InternallyDeveloped;
				VERDICT_Properties::adversariallyTestedForTrojanOrLogicBomb => 0;
			};
			controlSpeed: system ControlSpeed
			{
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => true;
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => InternallyDeveloped;
				VERDICT_Properties::adversariallyTestedForTrojanOrLogicBomb => 7;
			};
			filter: system Filter
			{
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => true;
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => InternallyDeveloped;
				VERDICT_Properties::adversariallyTestedForTrojanOrLogicBomb => 7;
			};
		
		connections
			c01: port on_off -> setSpeed.on_off
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c02: port resume_speed -> setSpeed.resume_speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c03: port set_speed -> setSpeed.set_speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c04: port inc -> setSpeed.inc
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c05: port dec -> setSpeed.dec
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c06: port brake_cancel -> setSpeed.brake_cancel
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c07: port measureSpeed.speed -> setSpeed.speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c08: port measureSpeed.speed -> controlSpeed.speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c09: port setSpeed.enabled -> enabled
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c10: port setSpeed.desired_speed -> filter.desired_speed
			{
				VERDICT_Properties::connectionType => Untrusted;
				VERDICT_Properties::deviceAuthentication => 0;
				VERDICT_Properties::encryptedTransmission => 0;
			};
			c11: port controlSpeed.force -> force
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c12: port rotate -> measureSpeed.rotate
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c13: port second -> measureSpeed.second
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c14: port filter.filtered_desired_speed -> controlSpeed.desired_speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			c15: port filter_is_enabled -> filter.is_enabled
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			-- Virtual connection for the sake of analysis
			v01: port filter.filtered_desired_speed -> desired_speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
			v02: port measureSpeed.speed -> speed
			{
				VERDICT_Properties::connectionType => Trusted;
			};
	end CruiseControllerSystem.Impl;
	
end CruiseController;
