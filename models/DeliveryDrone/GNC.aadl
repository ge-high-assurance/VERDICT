package GNC

public
	with Base_Types;
    with Data_Types;
    with Agree_Nodes;
    with VERDICT_Properties;

	system GPS
		features
			-- inputs
			constellation: in data port Data_Types::Constellation;
			satellite0_pos: in data port Data_Types::Position.impl;
			satellite1_pos: in data port Data_Types::Position.impl;
			
			-- outputs
			gps_pos: out data port Data_Types::Position.impl;
			health_status: out data port Base_Types::Boolean;
			probe_constellation: out data port Data_Types::Constellation
			{ VERDICT_Properties::probe => true; };
			
		annex agree {**
			 -- variables
			 eq satellite0_selected: bool = (constellation = enum(Data_Types::Constellation, Satellite0));
			 eq selected_satellite_pos: Data_Types::Position.impl = if satellite0_selected then 
			 												  			satellite0_pos 
			 												  		else satellite1_pos;
			 
			 -- high-level specification
			 guarantee "Output: gps_pos": Agree_Nodes.close_locations(gps_pos, selected_satellite_pos);
			 
			 -- probe outputs
			 guarantee "Output: probe_constellation": probe_constellation = constellation;
		**};
		
		annex verdict {**
			CyberRel "gps_out_I" => gps_pos:I;
			CyberRel "gps_out_A" => gps_pos:A;
			Event {
				id = "loa_event"
				probability = 1.0e-5
				comment = "loss of availability of the GPS"
				description = "LOA"
			}
			Event {
				id = "ued_event"
				probability = 1.0e-6
				comment = "undetected erroneous data of the GPS"
				description = "UED"
			}
			SafetyRel "gps_out_LOA" = happens("loa_event") => gps_pos:A;
			SafetyRel "gps_out_UED" = happens("ued_event") => gps_pos:I;
		**};
	end GPS;

	system IMU
		features
			-- inputs
			launch_pos: in data port Data_Types::Position.impl;
			
			-- outputs
			imu_pos: out data port Data_Types::Position.impl;
			health_status: out data port Base_Types::Boolean;
			probe_launch_pos: out data port Data_Types::Position.impl
			{ VERDICT_Properties::probe => true; };
		
		annex agree {**
			-- probe
			guarantee "Output: probe_launch_pos": probe_launch_pos = launch_pos;
		**};
		
		annex verdict {**
			CyberRel "imu_out_I" => imu_pos:I;
			CyberRel "imu_out_A" => imu_pos:A;
			Event {
				id = "loa_event"
				probability = 1.0e-5
				comment = "loss of availability of the IMU"
				description = "LOA"
			}
			Event {
				id = "ued_event"
				probability = 1.0e-6
				comment = "undetected erroneous data of the IMU"
				description = "UED"
			}
			SafetyRel "imu_out_LOA" = happens("loa_event") => imu_pos:A;
			SafetyRel "imu_out_UED" = happens("ued_event") => imu_pos:I;
		**};
	end IMU;
		
	system GNC
		features
			-- inputs
			constellation: in data port Data_Types::Constellation;
			satellite0_pos: in data port Data_Types::Position.impl;
			satellite1_pos: in data port Data_Types::Position.impl;
			launch_pos: in data port Data_Types::Position.impl;
			
			-- outputs
			gps_pos: out data port Data_Types::Position.impl;
			gps_health_status: out data port Base_Types::Boolean;
			probe_constellation: out data port Data_Types::Constellation
			{ VERDICT_Properties::probe => true; };
			imu_pos: out data port Data_Types::Position.impl;
			imu_health_status: out data port Base_Types::Boolean;
			probe_launch_pos: out data port Data_Types::Position.impl
			{ VERDICT_Properties::probe => true; };
			
		
	end GNC;
	
	system implementation GNC.Impl
		subcomponents
			gps: system GPS
			{
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => true; -- Another scenario - set this to false along with trustedConnection for c12 and c34 to false
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => COTS;
				VERDICT_Properties::dataReceivedFromUntrusted => true;
				
				-- VERDICT_Properties::manufacturer => InHouse; -- Fix for Logic Bomb / Hardware Trojan
				VERDICT_Properties::manufacturer => ThirdParty;	
				VERDICT_Properties::adversariallyTested => false;

                -- VERDICT Cyber Defenses & DAL Mitigations
				VERDICT_Properties::supplyChainSecurity => true;
				VERDICT_Properties::supplyChainSecurityDAL => 7;
				VERDICT_Properties::physicalAccessControl => true;
				VERDICT_Properties::physicalAccessControlDAL => 7;
				VERDICT_Properties::systemAccessControl => true;
				VERDICT_Properties::systemAccessControlDAL => 7;
				VERDICT_Properties::secureBoot => true;
				VERDICT_Properties::secureBootDAL => 7;
				VERDICT_Properties::memoryProtection => true;
				VERDICT_Properties::memoryProtectionDAL => 7;
				VERDICT_Properties::staticCodeAnalysis => true;
				VERDICT_Properties::staticCodeAnalysisDAL => 7;
				VERDICT_Properties::resourceAvailability => true;
				VERDICT_Properties::resourceAvailabilityDAL => 7;
				VERDICT_Properties::antiJamming => true;
				VERDICT_Properties::antiJammingDAL => 7;
				VERDICT_Properties::dosProtection => true;
				VERDICT_Properties::dosProtectionDAL => 7;
				VERDICT_Properties::deviceAuthentication => true;
				VERDICT_Properties::deviceAuthenticationDAL => 7;
				VERDICT_Properties::inputValidation => true;
				VERDICT_Properties::inputValidationDAL => 7;
				VERDICT_Properties::sessionAuthenticity => true;
				VERDICT_Properties::sessionAuthenticityDAL => 7;
				VERDICT_Properties::strongCryptoAlgorithms => true;
				VERDICT_Properties::strongCryptoAlgorithmsDAL => 7;																
			};
			imu: system IMU
			{ 
				-- VERDICT Component Properties
				VERDICT_Properties::insideTrustedBoundary => true;
				VERDICT_Properties::componentType => Hybrid;
				VERDICT_Properties::pedigree => Sourced;
				
				-- VERDICT Cyber Defenses & DAL Mitigations
				VERDICT_Properties::supplyChainSecurity => true;
				VERDICT_Properties::supplyChainSecurityDAL => 7;
				VERDICT_Properties::physicalAccessControl => true;
				VERDICT_Properties::physicalAccessControlDAL => 7;
				VERDICT_Properties::systemAccessControl => true;
				VERDICT_Properties::systemAccessControlDAL => 7;
				
				VERDICT_Properties::secureBoot => true;
				VERDICT_Properties::secureBootDAL => 7;
				VERDICT_Properties::memoryProtection => true;
				VERDICT_Properties::memoryProtectionDAL => 7;
			};
		connections
			i1: port constellation -> gps.constellation
			{VERDICT_Properties::flowType => Xdata;};
			i2: port satellite0_pos -> gps.satellite0_pos
			{VERDICT_Properties::flowType => Xdata;};
			i3: port satellite1_pos -> gps.satellite1_pos
			{VERDICT_Properties::flowType => Xdata;};
			i4: port launch_pos -> imu.launch_pos
			{VERDICT_Properties::flowType => Xdata;};
			i5: port gps.gps_pos -> gps_pos
			{VERDICT_Properties::flowType => Xdata;};
			i6: port gps.health_status -> gps_health_status
			{VERDICT_Properties::flowType => Xdata;};
			i7: port gps.probe_constellation -> probe_constellation
			{VERDICT_Properties::flowType => Xdata;};
			i8: port imu.imu_pos -> imu_pos
			{VERDICT_Properties::flowType => Xdata;};
			i9: port imu.health_status -> imu_health_status
			{VERDICT_Properties::flowType => Xdata;};
			i10: port imu.probe_launch_pos -> probe_launch_pos
			{VERDICT_Properties::flowType => Xdata;};
	end GNC.Impl;
end GNC;